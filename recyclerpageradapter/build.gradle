apply plugin: 'com.android.library'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        versionName VERSION
        versionCode = VERSION_CODE
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:support-v4:26.1.0'
}

apply plugin: 'com.jfrog.bintray'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apikey')

    configurations = ['archives']

    pkg {
        repo = 'Android'
        name = NAME
        userOrg = 'nyentek'

        licenses = ['Apache-2.0']
        vcsUrl = URL_GIT

        version {
            name = VERSION
            gpg {
                sign = true
                passphrase = properties.getProperty('bintray.gpg.password')
            }
        }
    }
}

apply plugin: 'com.github.dcendents.android-maven'

group = GROUP
version = VERSION

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'

            groupId GROUP
            artifactId ARTIFACT

            name NAME
            version VERSION
            description DESC
            url URL_WEB

            scm {
                developerConnection 'scm:git:' + URL_GIT
                connection 'scm:git:' + URL_GIT
                url URL_WEB
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    name 'Nyentek Team'
                    email 'contact@nyentek.com'
                    organization 'Nyentek'
                    organizationUrl 'http://www.nyentek.com'
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.source
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

    if (JavaVersion.current().isJava8Compatible()) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
